% 6. Generate the list of all arrangements of K elements of a given list.
% Eg: [2, 3, 4] K=2 => [[2,3], [3,2], [2,4], [4,2], [3,4], [4,3]]
%  (not necessary in this order)

% INSERT IN ALL POSITIONS an element into a list
% Mathematical model:
% insert(e, l1l2..ln) = 
%				e U l1l2..ln
%				l1 U insert(e, l2..ln)
% insert(E-element, L-list, R-result list)
% insert(i, i, o)
insert(E, L, [E|L]).
insert(E, [H|T], [H|R]) :-
    insert(E, T, R).


% ARRANGEMENTS of K elements from a list L
% Mathematical model: 
% arr(l1l2..ln, k) = 
%			l1, if k = 1 
%			arr(l2..ln, k), if k >= 1
%			insert(l1, arr(l2..ln, k-1)), if k > 1
% arr(L-list, K-nuber of elements, R-result list)	
% arr(i, i, o)

arr([E|_], 1, [E]).
arr([_|T], K, R) :- 
    arr(T, K, R).
arr([H|T], K, R) :-
    K > 1, 
    K1 is K-1,
    arr(T, K1, R1),
    insert(H, R1, R).

all_sols(L, K, RL):-
    findall(R, arr(L, K, R), RL).


% findall(R, arr([2, 3, 4], 2, R), RL)
% findall(R, arr([], 2, R), RL)